apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

android {
    signingConfigs {
        DefaultSigning {
            keyAlias localProperties['key_alias']?: ""
            keyPassword localProperties['key_password']?: ""
            storeFile file(localProperties['key_file']?: '')
            storePassword localProperties['key_password']?: ""
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.springcard.pcsclike_sample_ble"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode = (getRevisionMajor() + getRevisionMinor()).toInteger()
        def date = new Date()
        versionName = 'v' + date.format('yy.MM')
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "appDebug", "false"
            signingConfig signingConfigs.DefaultSigning
        }
        debug {
            buildConfigField "Boolean", "appDebug", "true"
        }
    }
    afterEvaluate {
        applicationVariants.all { variant ->
            variant.outputs.all {
                def buildType = variant.buildType.name
                def date = new Date()
                def formattedDate = date.format('yyyy_MM_dd')
                outputFileName = "SpringCardPcscOverBleAndroid_${getRevision()}_${formattedDate}_${buildType}.apk"
            }
        }
    }
    //https://youtrack.jetbrains.com/issue/KT-22430 could not import res from module
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation project(':PcscLikeSample')
    implementation project(':PcscLike')
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.5'
}
