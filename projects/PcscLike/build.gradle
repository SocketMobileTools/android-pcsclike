apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.archinamon.aspectj'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode = (getRevisionMajor() + getRevisionMinor()).toInteger()
        versionName getRevision()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "libraryName", "\"PC/SC-Like Library\""
        buildConfigField "String", "librarySpecial", "\"\""
        buildConfigField "String", "libraryVersion", "\""+getRevision()+"\""
        buildConfigField "int", "libraryVersionMajor", getRevisionMajor()
        buildConfigField "int", "libraryVersionMinor", getRevisionMinor()
        buildConfigField "int", "libraryVersionBuild", getRevisionBuild()
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "libraryDebug", "false"
        }
        debug {
            buildConfigField "Boolean", "libraryDebug", "true"
        }
    }
    afterEvaluate {
        android.libraryVariants.all { variant ->
            variant.outputs.all {
                def buildType = variant.buildType.name
                def date = new Date()
                def formattedDate = date.format('yyyy_MM_dd')
                outputFileName = "SpringCardPcscAndroidLibrary_${getRevision()}_${formattedDate}_${buildType}.aar"
            }
        }
    }


    task publishDoc(type: Copy) {
        from "$buildDir/javadoc"
        into 'V:\\Dossiers\\SpringCard\\A-Online\\apis\\Android\\PCSC-Like'
        /*exec {
            commandLine 'V:\\Dossiers\\SpringCard\\A-Online\\publier-api-android.cmd'
        }*/
    }

    dokka {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }
    dokka.finalizedBy publishDoc
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

repositories {
    mavenCentral()
}
